apply plugin: 'idea'

buildscript {
  repositories {
    mavenLocal()
    jcenter()
  }
  dependencies {
    classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.3.0.RELEASE'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.12.0'
    classpath 'com.moowork.gradle:gradle-grunt-plugin:0.5'
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.14'
}

configure(subprojects) {
  group = "org.ghipster"
}

project(':frontend') {
  apply plugin: 'grunt'

// makes sure on each build that grunt is installed
  grunt_build.dependsOn 'installGrunt'

// processes your package.json before running grunt build
  grunt_build.dependsOn 'npmInstall'

  task(build) << {
    println "ui built complete"
  }

  build.dependsOn grunt_build

  task clean(type: Delete) {
    delete 'dist'
    delete '.tmp'
  }

//    apply plugin: 'gulp'
//    apply plugin: NpmInstallPlugin
//    build.dependsOn gulp_build
//
//    task gulp_run(dependsOn: ['gulp_default'])
//    task bower(dependsOn: ['gulp_bower'])
}

project(':backend') {
  apply plugin: 'com.github.ben-manes.versions'
  apply plugin: 'groovy'
  apply plugin: 'java'
  apply plugin: 'idea'
  apply plugin: 'spring-boot'
  apply plugin: 'war'

  task cleanDist(type: Delete) {
    delete 'src/main/webapp'
  }

  clean.dependsOn cleanDist

  task copyDist(type: Copy) {
    from project(':frontend').file('dist')
    into 'src/main/webapp'
  }

  copyDist.dependsOn cleanDist

  compileJava.dependsOn ':frontend:build'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  war {
    baseName = 'gHipster'
    version = '0.1.0'
  }

  war.dependsOn copyDist

  repositories {
    jcenter()
  }

  dependencies {
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.codehaus.groovy:groovy-all:2.1.7"
    compile 'com.google.guava:guava:16.0.1'

    //js
    compile 'org.webjars:bootstrap:3.1.1'
    compile 'org.webjars:angularjs:1.2.14'
    compile 'org.webjars:underscorejs:1.6.0-1'
    //test
    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
    //e2e test
    testCompile "org.gebish:geb-spock:0.9.2"
  }
//
//    springBoot {
//        mainClass = "pl.jan.Application"
//    }
}