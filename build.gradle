import com.moowork.gradle.node.task.NodeTask
import org.gradle.api.Script

apply plugin: 'idea'

buildscript {
  repositories {
    mavenLocal()
    jcenter()
  }
  dependencies {
    classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.3.0.RELEASE'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.12.0'
    classpath 'com.moowork.gradle:gradle-node-plugin:0.12'
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.14'
}

configure(subprojects) {
  group = "org.ghipster"
}

project(':frontend') {
  apply plugin: "com.moowork.node"

  node {
    version = '5.8.0'
    npmVersion = '3.8.0'
    download = true
    // distBaseUrl = Custom artifactory location here.
  }

  task buildWebpack(type: NpmTask, dependsOn: 'npmInstall') {
    // install the express package only
    args = ['run', 'build']
  }

  task(build) << {
    println "ui built complete"
  }

  build.dependsOn buildWebpack

  task clean(type: Delete) {
    delete 'dist'
    delete '.tmp'
  }

}

project(':backend') {
  apply plugin: 'com.github.ben-manes.versions'
  apply plugin: 'groovy'
  apply plugin: 'java'
  apply plugin: 'idea'
  apply plugin: 'spring-boot'
  apply plugin: 'war'

  task cleanDist(type: Delete) {
    delete 'src/main/resources/public'
  }

  clean.dependsOn cleanDist

  task copyDist(type: Copy) {
    from project(':frontend').file('dist')
    into 'src/main/resources/public'
  }

  copyDist.dependsOn cleanDist

  compileJava.dependsOn ':frontend:build'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  war {
    baseName = 'gHipster'
    version = '0.1.0'
  }

  war.dependsOn copyDist

  repositories {
    jcenter()
  }

  dependencies {
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.codehaus.groovy:groovy-all:2.1.7"
    compile 'com.google.guava:guava:16.0.1'

    //js
    compile 'org.webjars:bootstrap:3.1.1'
    compile 'org.webjars:angularjs:1.2.14'
    compile 'org.webjars:underscorejs:1.6.0-1'
    //test
    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
    //e2e test
    testCompile "org.gebish:geb-spock:0.9.2"
  }
//
//    springBoot {
//        mainClass = "pl.jan.Application"
//    }
}